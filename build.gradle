plugins {
    // Apply the application plugin to add support for building a CLI application.
    id 'application'

    // Apply the Kotlin JVM plugin to add support for Kotlin.
    id 'org.jetbrains.kotlin.jvm' version '1.5.31'

    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'net.nemerosa.versioning' version '2.6.1'
}

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    // Align versions of all Kotlin components
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0'

    implementation 'com.vladsch.flexmark:flexmark-all:0.64.0'
    implementation 'com.vladsch.flexmark:flexmark-test-util:0.64.0'
    implementation 'com.vladsch.flexmark:flexmark-ext-footnotes:0.64.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-cli:0.3.4'

    implementation 'org.apache.poi:poi:5.+'
    implementation 'org.apache.poi:poi-ooxml:5.+'
    implementation 'org.apache.poi:poi-scratchpad:5.+'

    // Use the Kotlin test library & JUnit
    testImplementation 'org.jetbrains.kotlin:kotlin-test:1.6.0'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit:1.6.0'
}

application {
    // Define the main class for the application.
    mainClassName = 'com.newardassociates.pptbuilder.AppKt'
}

jar {
    manifest {
        attributes (
            'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd").format(new Date()),
            'Build-Revision': versioning.info.commit
        )
    }
}

shadowJar {
    archiveClassifier = ''
    mergeServiceFiles()
}
